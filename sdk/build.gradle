apply plugin: 'com.android.library'

group = 'com.desk'
version = '1.3.2'
if (System.properties['version'] != null) {
    version = System.properties['version'] as String;
}

ext {
    projectName = 'Desk Android SDK'
    bintrayOrg = 'desk'
    bintrayRepo = 'public'
    bintrayName = 'sdk'
    siteUrl = 'https://github.com/forcedotcom/DeskMobileSDK-Android'
    gitUrl = 'scm:git:git@github.com:forcedotcom/DeskMobileSDK-Android.git'
    licenseName = 'The BSD 3-Clause License'
    licenseUrl = 'http://opensource.org/licenses/BSD-3-Clause'
    tag = 'v' + version
}

android {
    compileSdkVersion projectCompileSdkVersion
    buildToolsVersion projectBuildToolsVersion

    defaultConfig {
        minSdkVersion projectMinSdkVersion
        targetSdkVersion projectTargetSdkVersion
        versionCode 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
        exclude 'META-INF/LICENSE.txt'
    }
}

//noinspection GroovyAssignabilityCheck
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 1, 'hours' // refresh changing modules every hour
}

ext {
    versions = [supportLibrary    : '24.0.0',
                testRules         : '0.5',
                espresso          : '2.2.2',
                mockito           : '1.2',
                mockitoCore       : '1.10.19',
                assertj           : '1.1.1',
                junit             : '4.12'
    ]
}

dependencies {
    compile "com.android.support:support-annotations:$versions.supportLibrary"
    compile "com.android.support:appcompat-v7:$versions.supportLibrary"
    compile "com.desk:api-client:1.4.3"

    // instrumentation test dependencies
    androidTestCompile "com.android.support.test:runner:$versions.testRules"
    androidTestCompile "com.android.support.test:rules:$versions.testRules"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$versions.espresso"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$versions.espresso"
    androidTestCompile "com.squareup.assertj:assertj-android:$versions.assertj"
    androidTestCompile "org.mockito:mockito-core:$versions.mockitoCore"
    androidTestCompile "com.google.dexmaker:dexmaker:$versions.mockito"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:$versions.mockito"

    // unit test dependencies
    testCompile "junit:junit:$versions.junit"
    testCompile "org.mockito:mockito-core:$versions.mockitoCore"
}

apply from: '../credentials.gradle'
apply from: '../compile.gradle'
apply from: '../publishing.gradle'
apply from: '../git-tag.gradle'
apply from: '../bintray-upload.gradle'
apply from: '../artifactory.gradle'
